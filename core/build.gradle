plugins {
    id 'application'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

group = 'io.github.liveisgood8'
archivesBaseName = 'jackson-versioning-module'
version = '2.0.0'

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

ext {
    junitVersion = '5.7.2'
    jacksonVersion = '2.13.0'
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jackson-versioning-module'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Jackson versioning module'
                description = 'Module which add ability to versioning properties on serialization to json.'
                url = 'https://github.com/liveisgood8/jackson-versioning-module'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'liveisgood8'
                        name = 'Kirill Lemeshonok'
                        email = 'lemeshonok12@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/liveisgood8/jackson-versioning-module.git'
                    developerConnection = 'scm:git:ssh://github.com/liveisgood8/jackson-versioning-module.git'
                    url = 'https://github.com/liveisgood8/jackson-versioning-module'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            def currentSonatypeUsername = project.hasProperty('SONATYPE_USER')
                    ? project.property('SONATYPE_USER').toString()
                    : 'SONATYPE_USER'
            def currentSonatypePassword = project.hasProperty('SONATYPE_PASSWORD')
                    ? project.property('SONATYPE_PASSWORD').toString()
                    : 'SONATYPE_PASSWORD'

            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username currentSonatypeUsername
                password currentSonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
